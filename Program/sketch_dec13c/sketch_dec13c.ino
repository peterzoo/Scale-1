// OLED libraries
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

// load cell library
#include "HX711.h"

// OLED config
constexpr int SCREEN_WIDTH  = 128;
constexpr int SCREEN_HEIGHT = 64;
constexpr int OLED_SDA = 21;
constexpr int OLED_SCL = 22;
constexpr uint8_t OLED_ADDRESS = 0x3C;

Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, -1);

// HX711 config
// DAT/DOUT is output from HX711 to ESP32, CLK/SCK is clock from ESP32 to HX711
constexpr int HX711_DAT = 19;
constexpr int HX711_CLK = 18;

HX711 scale;

// UI pins
constexpr int ZERO_BUTTON_PIN = 27;
constexpr int MODE_PIN = 26;
constexpr int BUZZER_PIN = 25;

// calibration constant via manual tuning w/known weight
const float calFactor = 748;

// -------------------------
//    Function Prototypes
// -------------------------

void beep(int); // buzzer beep
void quantize(float); // quantize values to nearest 0.1 g
void hysteresis(float); // restrict screen updates if change is too small

void setup() {
  Serial.begin(115200);
  delay(200);

  // init UI pins
  pinMode(BUZZER_PIN, OUTPUT);
  pinMode(ZERO_BUTTON_PIN, INPUT_PULLUP);
  pinMode(MODE_PIN, INPUT_PULLUP);

  // start I2C
  Wire.begin(OLED_SDA, OLED_SCL);

  // init OLED
  if (!display.begin(SSD1306_SWITCHCAPVCC, OLED_ADDRESS)) {
    Serial.println("SSD1306 init failed");
    while (true) { delay(1000); }
  }

  display.clearDisplay();
  display.setTextColor(SSD1306_WHITE);

  // Init HX711
  scale.begin(HX711_DAT, HX711_CLK);
  scale.set_gain(128);
  scale.set_scale(calFactor);

  // Startup screen
  display.setTextSize(2);
  display.setCursor(0, 0);
  display.println("Scale (1)");
  display.display();
  beep(120);
  delay(800);

  // Tare on startup (make sure nothing is on the scale)
  display.clearDisplay();
  display.setTextSize(1);
  display.setCursor(0, 0);
  display.println("Taring...");
  display.display();

  scale.tare();
  beep(200);
  display.clearDisplay();
  display.setCursor(0, 0);
  display.println("Ready.");
  display.display();
  delay(300);
  
}

void loop() {
  // Button is INPUT_PULLUP => pressed = LOW
  if (digitalRead(ZERO_BUTTON_PIN) == LOW) {
    // Basic debounce
    delay(25);
    if (digitalRead(ZERO_BUTTON_PIN) == LOW) {
      scale.tare();
      beep(150);

      // Wait for release so it doesn't spam tare
      while (digitalRead(ZERO_BUTTON_PIN) == LOW) {
        delay(10);
      }
    }
  }

  long val = 0;
  float grams = 0.0f;
  static float gFilt = 0;

  if (scale.is_ready()) {
    val = scale.get_value();
    grams = scale.get_units(1);
  }

  gFilt = 0.5f*gFilt + 0.5f*grams;

  // Update OLED
  display.clearDisplay();

  display.setTextSize(1);
  display.setCursor(0, 0);
  display.print("Raw:");

  // display.setTextSize(1);
  // display.setCursor(0, 16);
  // display.print(raw);

  display.setTextSize(1);
  display.setCursor(0, 16);
  display.print(val);

  display.setTextSize(2);
  display.setCursor(45, 16);
  display.print(grams,1);
  display.println(" g");

  display.setTextSize(2);
  display.setCursor(0, 40);
  display.print(gFilt,1);

  display.display();

  delay(70);
}

// ---------------
//    Functions
// ---------------

// beep
void beep(int ms = 15) {
  digitalWrite(BUZZER_PIN, HIGH);
  delay(ms);
  digitalWrite(BUZZER_PIN, LOW);
}

void quantize(float); // quantize values to nearest 0.1 g
void hysteresis(float); // restrict screen updates if change is too small
