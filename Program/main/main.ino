// OLED libraries
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

// load cell library
#include "HX711.h"

// OLED config
constexpr int SCREEN_WIDTH  = 128;
constexpr int SCREEN_HEIGHT = 64;
constexpr int OLED_SDA = 21;
constexpr int OLED_SCL = 22;
constexpr uint8_t OLED_ADDRESS = 0x3C;

Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, -1);

// HX711 config
// DAT/DOUT is output from HX711 to ESP32, CLK/SCK is clock from ESP32 to HX711
constexpr int HX711_DAT = 19;
constexpr int HX711_CLK = 18;

HX711 scale;

// UI pins
constexpr int zeroButtonPin = 27;
constexpr int modeButtonPin = 26;
constexpr int buzzerPin = 25;

// calibration constant via manual tuning w/known weight
const float calFactor = 734;

// -------------------------
//    function prototypes
// -------------------------

void beep(int); // buzzer beep
float quantize(float g); // quantize values to nearest 0.1 g
float hysteresis(float read_g); // restrict screen updates if change is too small
float varZeroClamp(float g); // clamp values close to 0
void tare(unsigned long nowTime, float &gFilt, bool &running, unsigned long &flowStopTimer, float &time, bool &startOnce); // tare scale

// tuning knobs
const float emaBig = 0.4; // ema for large changes
const float emaMed = 0.7; // ema for medium changes
const float emaSma = 0.95; // ema for tiny changes/noise
const float threshold = 0.08; // hysteresis threshold for movement
const int sma = 3; // sma N value for scale.get_value
const float zClampPos = 0.2; // positive z clamp value
const float zClampNeg = 0.3; // negative z clamp value
const float timerStartG = 2.0; // gram weight for timer to start
unsigned long minFlowT = 800; // ms without flow for timer to stop
const float minFlowG = 0.2; // gram minimum flow change for timer to stop

// timer
unsigned long tStart = 0; // ms since boot
unsigned long flowStopTimer = 0; // ms tracker to stop flow

// millis based timing
const unsigned long refresh = 30; // ms between executing loop
const unsigned long debounce = 25; // ms for debounce

// ------------- FSM --------------
enum Mode {
  MODE_KITCHEN, // 0: weight only
  MODE_SHOT,    // 1: weight + auto start-stop timer
  // MODE_POUR,    // 2: weight + cts timer
  // MODE_MANUAL,  // 3: weight + manual press timer
  // MODE_SLEEP,   // 4: low power "off"
  MODE_COUNT    // 5: counts total number of modes for cycling
};

static Mode mode = MODE_KITCHEN;
// --------------------------------

void setup() {
  Serial.begin(115200);
  delay(200);

  // init UI pins
  pinMode(buzzerPin, OUTPUT);
  pinMode(zeroButtonPin, INPUT_PULLUP);
  pinMode(modeButtonPin, INPUT_PULLUP);

  // start I2C
  Wire.begin(OLED_SDA, OLED_SCL);

  // init OLED
  if (!display.begin(SSD1306_SWITCHCAPVCC, OLED_ADDRESS)) {
    Serial.println("SSD1306 init failed");
    while (true) { delay(1000); }
  }

  display.clearDisplay();
  display.setTextColor(SSD1306_WHITE);

  // Init HX711
  scale.begin(HX711_DAT, HX711_CLK);
  scale.set_gain(128);
  scale.set_scale(calFactor);

  // Startup screen
  display.setTextSize(2);
  display.setCursor(0, 0);
  display.println("Scale (1)");
  display.display();
  beep(120);
  delay(800);

  // Tare on startup (make sure nothing is on the scale)
  display.clearDisplay();
  display.setTextSize(1);
  display.setCursor(0, 0);
  display.println("Taring...");
  display.display();

  scale.tare();
  beep(200);
  display.clearDisplay();
  display.setCursor(0, 0);
  display.println("Ready.");
  display.display();
  delay(300);
  
}

void loop() {
  // define weigh variables
  long val = 0;
  float grams = 0.0f;
  static float gFilt = 0;
  static float prevGFilt = 0; // timer auto-stop derivative

  // timer variables
  static bool running = false; // state of timer, running or not
  static float time = 0;
  static bool startOnce = true; // timer can only start once, in beginning

  // millis based refresh
  static unsigned long lastTime = 0;
  unsigned long nowTime = millis();

  // -------------- FSM ---------------
  static bool lastModeButton = HIGH;

  bool modeButton = digitalRead(modeButtonPin);
  static unsigned long lastModePress = 0;
  if (lastModeButton == HIGH && modeButton == LOW) {
    if (nowTime - lastModePress > debounce) { // debounce
      lastModePress = nowTime;

    // cycle modes
      mode = (Mode)((mode+1) % MODE_COUNT);
      beep(100);
    }
  }

  lastModeButton = modeButton;

  //--------------- FSM end-------------

  if (nowTime - lastTime >= refresh) {
      lastTime = nowTime;
    
    // millis based zero function
    tare(nowTime, gFilt, running, flowStopTimer, time, startOnce);

    if (scale.is_ready()) {
      val = scale.get_value(1);
      grams = scale.get_units(sma);
    }

    // --------- raw reading processing -------------

    // adaptive EMA
    float err = fabsf(grams - gFilt);

    if(err > 1.0f) {
      gFilt = grams; // snap immediately for large change
    }
    else {
      float ema = 0;
      if (err > 0.3f)         {ema = emaBig;}  // moving fast
      else if (err > 0.1f)   {ema = emaMed;}  // moving medium
      else                    {ema = emaSma;} // still/no change

      gFilt = ema * gFilt + (1.0f - ema) * grams;
    }
    
    gFilt = hysteresis(gFilt); // Controls when UI can change to ignore noise
    gFilt = varZeroClamp (gFilt);
    gFilt = quantize(gFilt); // quantize to fixed steps

    switch (mode) {
      case MODE_KITCHEN:
        // disable timer, weight only
        running = false;
        time = 0.0f;
        startOnce = true;
        flowStopTimer = 0;
        break;

      case MODE_SHOT:
        // auto-stop shot timer enabled
        if (running == false && startOnce == true && gFilt > timerStartG)
        {
          running = true;
          tStart = nowTime;
          time = 0.0f;
          startOnce = false;
          flowStopTimer = 0;
        }

        // auto stop checker
        if (running == true) {
          float deltaG = gFilt - prevGFilt; // change in weight

          if (fabsf(deltaG) < minFlowG) { // flow stopped, weight barely changing
            if (flowStopTimer == 0) {
              flowStopTimer = nowTime; // start counting flow stopped time
            } else if (nowTime - flowStopTimer > minFlowT){ // if flow stop timer is running
              running = false;
              startOnce = false;
              flowStopTimer = 0;
            }
          } else {
              flowStopTimer = 0;
          }
        }

        if (running == true) {
          time = (nowTime - tStart)/1000.0f;
        }
      
      // re-arm timer if cup is removed
      if (!running && gFilt < 1.0f) {
        startOnce = true;
      }
      break;
    }

    prevGFilt = gFilt;

    // Update OLED
    display.clearDisplay();

    // display mode
    display.setTextSize(1);
    display.setCursor(0, 0);
    display.print("Mode:");
    if (mode == MODE_KITCHEN) display.print("KITCHEN");
    else if (mode == MODE_SHOT) display.print("SHOT");

    // raw value
    display.setTextSize(1);
    display.setCursor(0, 16);
    display.print(val);

    // raw grams
    display.setTextSize(2);
    display.setCursor(55, 16);
    display.print(grams,2);

    // filtered grams
    display.setTextSize(2);
    display.setCursor(0, 40);
    display.print(gFilt,1);

    // time
    display.setTextSize(2);
    display.setCursor(55,40);
    if (mode == MODE_SHOT) display.print(time,1);

    display.display();
  }
}

// ---------------
//    Functions
// ---------------

// beep
void beep(int ms = 15) {
  digitalWrite(buzzerPin, HIGH);
  delay(ms);
  digitalWrite(buzzerPin, LOW);
}

// quantize values to nearest 0.1 g
float quantize(float g) {
  return roundf(g * 10.0f) / 10.0f;
}

// restrict screen updates if change is too small
float hysteresis(float read_g) {
  static float shown_g = 0.00f;
  if (fabsf(read_g - shown_g) >= threshold) {
    shown_g = read_g;
  }
  return shown_g;
}

// locks values close enough to 0.0 to display 0.0 for UX
float varZeroClamp(float g) {
  if (g < 0 && fabsf(g) < zClampNeg) return 0.0f;
  if (g > 0 && fabsf(g) < zClampPos) return 0.0f;

  return g;
}

void tare(unsigned long nowTime, float &gFilt, bool &running, unsigned long &flowStopTimer, float &time, bool &startOnce) {
  static unsigned long lastZero = 0;
  static bool lastState = HIGH;

  bool state = digitalRead(zeroButtonPin);

  if (lastState == HIGH && state == LOW) {
    if (nowTime - lastZero > debounce) {
      lastZero = nowTime;

      // reset everything
      scale.tare();
      gFilt = 0;
      running = false;
      time = 0;
      flowStopTimer = 0;
      startOnce = true;
      beep(100);
    }
  }

  lastState = state;
}